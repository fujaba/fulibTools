plugins {
   id 'java-library'
   id 'maven-publish'
   // https://plugins.gradle.org/plugin/com.jfrog.bintray
   id 'com.jfrog.bintray' version '1.8.5'
   // https://plugins.gradle.org/plugin/de.clashsoft.simple-publish
   id 'de.clashsoft.simple-publish' version '0.6.0'
   // https://plugins.gradle.org/plugin/com.palantir.revapi
   id 'com.palantir.revapi' version '1.4.3'
   // https://plugins.gradle.org/plugin/org.fulib.fulibGradle
   id 'org.fulib.fulibGradle' version '0.4.0'
}

group = 'org.fulib'
version = 'git describe --tags'.execute().text[1..-2] // strip v and \n
description = '''FulibTools provides some additional features like class diagrams and object diagrams to the Fulib 
code generator. These additional features require some large dependencies and as not every user benefits from them, 
we seperated these additional features from Fulib into FulibTools.'''

publishInfo {
   organization = 'fujaba'
   githubRepo = 'fujaba/fulibTools'
   labels = [ 'fulib', 'code-generator', 'tools' ]

   developer {
      id = 'zuendorf'
      name = 'Albert Zuendorf'
   }
   developer {
      id = 'digitalhoax'
      name = 'Tobias George'
   }
   developer {
      id = 'eicke123'
      name = 'Christoph Eickhoff'
   }

   license {
      name = 'MIT License'
      url = 'http://www.opensource.org/licenses/mit-license.php'
   }
}

sourceCompatibility = 1.8

// --------------- Dependencies ---------------

repositories {
   mavenLocal()
   mavenCentral()
   jcenter()
}

dependencies {
   // https://mvnrepository.com/artifact/org.fulib/fulibScenarios
   fulibScenarios group: 'org.fulib', name: 'fulibScenarios', version: '1.4.0'

   // https://mvnrepository.com/artifact/org.fulib/fulib
   api group: 'org.fulib', name: 'fulib', version: '1.4.1'

   // https://mvnrepository.com/artifact/org.apache.commons/commons-text
   implementation group: 'org.apache.commons', name: 'commons-text', version: '1.9'

   // https://mvnrepository.com/artifact/org.antlr/ST4
   implementation group: 'org.antlr', name: 'ST4', version: '4.3.1'

   // https://mvnrepository.com/artifact/guru.nidi/graphviz-java
   implementation group: 'guru.nidi', name: 'graphviz-java-all-j2v8', version: '0.17.0'

   // https://mvnrepository.com/artifact/org.junit/junit
   testImplementation group: 'junit', name: 'junit', version: '4.13'

   // https://mvnrepository.com/artifact/commons-io/commons-io
   testImplementation group: 'commons-io', name: 'commons-io', version: '2.8.0'
}

// --------------- Misc. Settings ---------------

// make the studyRight classModel.yaml available as a resource
sourceSets.test.resources.srcDir 'src/test/java'

javadoc {
   failOnError = false
}

revapi {
   oldVersion = '1.3.0'
}

test {
   testLogging.showStandardStreams = true

   workingDir = temporaryDir
   outputs.dir workingDir

   doFirst {
      workingDir.mkdirs()
   }
}
