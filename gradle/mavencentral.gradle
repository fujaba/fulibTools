

// Create a jar with javadoc
task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

// Create a jar with soruces
task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

// hook all the artifacts under artifacts task
artifacts {
  archives sourcesJar
}

// Signing task
signing {
  sign configurations.archives
}

// `FOO` and `BAR` are overridden by external ones!
def v_ossrhUsername="FOO"
def v_ossrhPassword="BAR"

if (project.hasProperty("ossrhUsername")) {
  v_ossrhUsername = ossrhUsername
}
if (project.hasProperty("ossrhPassword")) {
  v_ossrhPassword = ossrhPassword
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: v_ossrhUsername, password: v_ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: v_ossrhUsername, password: v_ossrhPassword)
      }

      pom.project {
        name 'fulibTools'
        packaging 'jar'
        artifactId archivesBaseName
        version version
        description 'FulibTools provides some additional features like class diagrams and object diagrams to the Fulib code generator. These additional features require some large dependencies and as not every user benefits from them, we seperated these additional features from Fulib into FulibTools.'
        url 'http://fulib.org'

        scm {
          connection 'scm:git:git://github.com/fujaba/fulibTools.git'
          developerConnection 'scm:git:git@github.com:/fujaba/fulibTools.git'
          url 'https://github.com/fujaba/fulib'
        }

        licenses {
          license {
            name 'MIT License'
            url 'http://www.opensource.org/licenses/mit-license.php'
          }
        }

        developers {
							developer {
								id 'zuendorf'
								name 'Albert Zuendorf'
							}
							developer {
								id 'digitalhoax'
								name 'Tobias George'
							}
							developer {
								id 'eicke123'
								name 'Christoph Eickhoff'
							}
						}
      }
    }
  }
}
